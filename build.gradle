plugins {
	id "fabric-loom" version "1.6-SNAPSHOT"
}

version = project.mod_version
group = project.maven_group

repositories {
	maven { url "https://maven.shedaniel.me/" } //cloth-config
	maven { url "https://maven.terraformersmc.com" } //modmenu
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api}"
	
	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config}") { exclude(group: "net.fabricmc.fabric-api") }
	modApi "com.terraformersmc:modmenu:${project.modmenu_version}"
}

base {
	archivesName = project.archives_base_name
}

processResources {
    def expandProps = [
            "version": rootProject.mod_version,
            "minecraft_version_range": rootProject.minecraft_version_range,
    ]

    filesMatching(["fabric.mod.json"]) {
        expand expandProps
    }

    inputs.properties(expandProps)
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}
